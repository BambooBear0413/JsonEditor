plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'application'
    id 'io.github.crimix.replace-placeholders' version '3.0'
}

group = 'dev.bamboobear'
version = project.app_version

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.13.0' exclude(group: 'com.google.errorprone', module: 'error_prone_annotations')
}

application {
    mainClass = 'io.bamboobear.json_editor.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    archiveBaseName = project.app_name
    destinationDirectory = layout.buildDirectory

    manifest {
        attributes (
                'Main-Class': application.mainClass,
                'Class-Path': project.configurations.runtimeClasspath.files
                        .collect {"libs/${it.name}" }
                        .join(' '),
                'Implementation-Title': project.app_name,
                'Implementation-Version': project.version
        )
    }
}

replaceSourcePlaceholders {
    enabled true
    filesToExpand "${application.mainClass.get().replace('.', '/')}.java"
    extraProperties 'app_version'
}

tasks.register('copyDependencies', Copy.class) {
    from project.configurations.runtimeClasspath
    into layout.buildDirectory.dir("libs")
}

tasks.build.dependsOn('copyDependencies')