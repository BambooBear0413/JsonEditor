plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'application'
}

group = 'dev.bamboobear'
version = '0.0.1-alpha.1'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.10.1'
}

application {
    mainClass = 'io.bamboobear.json_editor.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    archiveBaseName = project.app_name
    destinationDirectory = layout.buildDirectory

    manifest {
        attributes (
                'Main-Class': application.mainClass,
                'Class-Path': project.configurations.runtimeClasspath.files
                        .collect {"libs/${it.name}" }
                        .join(' '),
                'Implementation-Title': project.app_name,
                'Implementation-Version': project.version
        )
    }
}

tasks.register('copyDependencies', Copy.class) {
    from project.configurations.runtimeClasspath
    into layout.buildDirectory.dir("libs")
}

tasks.build.dependsOn('copyDependencies')