plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'io.github.crimix.replace-placeholders' version '3.0'
}

group = 'dev.bamboobear'
version = project.app_version

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.13.0' exclude(group: 'com.google.errorprone', module: 'error_prone_annotations')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withJavadocJar()
    withSourcesJar()
}

jar {
    archiveBaseName = project.app_name
    destinationDirectory = layout.buildDirectory

    manifest {
        attributes (
                'Main-Class': project.main_class,
                'Class-Path': project.configurations.runtimeClasspath.files
                        .collect {"libs/${it.name}" }
                        .join(' '),
                'Implementation-Title': project.app_name,
                'Implementation-Version': project.version
        )
    }
}

replaceSourcePlaceholders {
    enabled true
    filesToExpand "${project.main_class.replace('.', '/')}.java"
    extraProperties 'app_version'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.app_name
            }
        }
    }
}

tasks.register('copyDependencies', Copy) {
    from project.configurations.runtimeClasspath
    into layout.buildDirectory.dir("libs")
}

tasks.register('run', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = project.main_class
}

tasks.build.dependsOn('copyDependencies')